;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-

(setq user-full-name "Daniil Rose"
      user-mail-address "daniil.rose@posteo.org")

;; Load private scripts
(add-to-list 'load-path "~/.doom.d/lisp/")

;; Workaround for SVG error
(setq image-types (cons 'svg image-types))

;; Custom feature flags
(setq catster/use-mail nil)
(setq catster/use-exwm nil)
(setq catster/use-irc nil)

;; I keep all my org mode notes synced with my iPad Air and Pixel 7 Pro.
(setq org-directory "~/Nextcloud/org/")

;; I do not care to constantly see lines, and the performance boost is nice.
(setq display-line-numbers-type t)

;; I get it, some deprecation messages will always be there.
(setq inhibit-startup-message t)

;; Hide modeline in treemacs
(add-hook 'treemacs-mode-hook 'hide-mode-line-mode)

;; JetBrains Mono has been my favourite font since first learning to program
(setq doom-font (font-spec :family "JetBrainsMono Nerd Font" :size 14)
      ivy-posframe-font (font-spec :family "JetBrainsMono Nerd Font" :size 15))

;; Nice things to see related to windows, and to use trash instead of perma delete.
(setq-default
 window-combination-resize t
 delete-by-moving-to-trash t
 x-stretch-cursor t)

;; Give me as much undo as you can.
(setq undo-limit 80000000
      auto-save-default nil
      truncate-string-ellipsis "‚Ä¶"
      scroll-margin 20)

;; Subword mode is nice.
(with-eval-after-load 'subword
  (diminish 'subword-mode))
(global-subword-mode 1)

;; Keep files up to date.
(setq global-auto-revert-non-file-buffers t)
(global-auto-revert-mode 1)

;; Scrolling should be one line at a time.
(setq scroll-step 1)

;; Fix SSL issues
(setq gnutls-min-prime-bits 1024)
(setq gnutls-algorithm-priority "SECURE128:-VERS-SSL3.0:-VERS-TLS1.3")
(setq ssl-certificate-directory "/etc/ssl/certs")
(setq ssl-program-name "gnutls-cli")
(setq ssl-program-arguments
      '("--port" service
        "--x509cafile" "/etc/ssl/certs/ca-certificates.crt"
        "--priority" "SECURE:-VERS-SSL3.0"
        "--priority" "SECURE128:-VERS-SSL3.0:-VERS-TLS1.3"
        host))

;; Disable invasive lsp-mode features
(setq lsp-ui-sideline-enable nil   ; not anymore useful than flycheck
      lsp-ui-doc-enable nil        ; slow and redundant with K
      lsp-enable-symbol-highlighting nil
      +lsp-prompt-to-install-server 'quiet)

;; Nice features when I am on my MacBook Pro 16", which is essentially always.
(when IS-MAC
  (setq mac-option-key-is-meta nil
        mac-command-key-is-meta t
        mac-command-modifier 'meta
        mac-option-modifier 'super))

;; Nice to see the time.
(display-time-mode 1)

;; Do not bring up key recipient dialogue.
(require 'epa-file)
(setq epa-file-select-keys 1)
(setq epa-file-encrypt-to '("<daniil.rose@member.fsf.org>"))

;; Increase the password cache expiry time, technically does not do anything for GPG2
(setq password-cache-expiry (* 60 15))

;; Fix EasyPG error.
;; From https://colinxy.github.io/software-installation/2016/09/24/emacs25-easypg-issue.html.
(setf epg-pinentry-mode 'loopback)
(defun pinentry-emacs (desc prompt ok error)
  (let ((str (read-passwd
              (concat (replace-regexp-in-string "%22" "\""
                                                (replace-regexp-in-string "%0A" "\n" desc)) prompt ": "))))
    str))

;; Email configuration
(when catster/use-mail
  (add-to-list 'load-path "~/.doom.d/mu4e/")
  (require 'catsters-mail))

;; Improve org mode performance
(after! org
  (setq org-fontify-quote-and-verse-blocks nil
        org-fontify-whole-heading-line nil
        org-hide-leading-stars nil
        ))

;; Replace iSearch with Consult Line
(map! :desc "Search buffer" :g "C-s" #'consult-line)

;; Replace splash screen so people stop asking why I am playing a game
(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-shortmenu)
(remove-hook '+doom-dashboard-mode-hook #'(lambda () (hl-line-mode -1)))
(add-hook '+doom-dashboard-mode-hook 'hide-mode-line-mode)

(defun doom-dashboard-draw-ascii-emacs-banner-fn ()
  (let* ((banner
          '("      ___          ___          ___          ___          ___ "
            "     /\\__\\        /\\  \\        /\\  \\        /\\__\\        /\\__\\"
            "    /:/ _/_      |::\\  \\      /::\\  \\      /:/  /       /:/ _/_"
            "   /:/ /\\__\\     |:|:\\  \\    /:/\\:\\  \\    /:/  /       /:/ /\\  \\"
            "  /:/ /:/ _/_  __|:|\\:\\  \\  /:/ /::\\  \\  /:/  /  ___  /:/ /::\\  \\"
            " /:/_/:/ /\\__\\/::::|_\\:\\__\\/:/_/:/\\:\\__\\/:/__/  /\\__\\/:/_/:/\\:\\__\\"
            " \\:\\/:/ /:/  /\\:\\~~\\  \\/__/\\:\\/:/  \\/__/\\:\\  \\ /:/  /\\:\\/:/ /:/  /"
            "  \\::/_/:/  /  \\:\\  \\       \\::/__/      \\:\\  /:/  /  \\::/ /:/  /"
            "   \\:\\/:/  /    \\:\\  \\       \\:\\  \\       \\:\\/:/  /    \\/_/:/  /"
            "    \\::/  /      \\:\\__\\       \\:\\__\\       \\::/  /       /:/  /"
            "     \\/__/        \\/__/        \\/__/        \\/__/        \\/__/"
            ))
         (longest-line (apply #'max (mapcar #'length banner))))
    (put-text-property
     (point)
     (dolist (line banner (point))
       (insert (+doom-dashboard--center
                +doom-dashboard--width
                (concat
                 line (make-string (max 0 (- longest-line (length line)))
                                   32)))
               "\n"))
     'face 'doom-dashboard-banner)))

(setq +doom-dashboard-ascii-banner-fn #'doom-dashboard-draw-ascii-emacs-banner-fn)

;;; Configuring Packages Defined in ~packages.el~

(use-package! auto-sudoedit-mode
  :defer 1
  :diminish auto-sudoedit-mode
  :config
  (auto-sudoedit-mode 1))

(use-package! default-text-scale
  :defer 1
  :config
  (default-text-scale-mode))

(use-package! super-save
  :defer 1
  :diminish super-save-mode
  :config
  (super-save-mode +1)
  (setq super-save-auto-save-when-idle t))

(use-package! hackernews
  :commands (hackernews))

(use-package! tokei
  :commands (tokei))

(after! dap-mode
  (setq dap-python-debugger 'debugpy))

;;; PDF Tools
(add-hook! 'pdf-view-mode (pdf-view-themed-minor-mode 1))
(add-hook! 'pdf-view-mode (pdf-annot-minor-mode 0))
(add-hook! 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer)

;;; IRC
(when catster/use-irc
  (require 'irc-config))

;;; EXWM
(when catster/use-exwm
  (add-to-list 'load-path "~/.doom.d/exwm")
  (display-battery-mode 1)
  (require 'catsters-exwm))

;;; LaTeX
(use-package! latex
  :hook ((LaTeX-mode . prettify-symbols-mode))
  :bind (:map LaTeX-mode-map
              ("C-S-e" . latex-math-from-calc))
  :config
  ;; Format math as a Latex string with Calc
  (defun latex-math-from-calc ()
    "Evaluate calc on the contents of line at point."
    (interactive)
    (cond ((region-active-p)
           (let* ((beg (region-beginning))
                  (end (region-end))
                  (string (buffer-substring-no-properties beg end)))
             (kill-region beg end)
             (insert (calc-eval `(,string calc-language latex
                                  calc-prefer-frac t
                                  calc-angle-mode rad)))))
          (t (let ((l (thing-at-point 'line)))
               (end-of-line 1) (kill-line 0)
               (insert (calc-eval `(,l
                                    calc-language latex
                                    calc-prefer-frac t
                                    calc-angle-mode rad))))))))

(use-package preview
  :after latex
  :hook ((LaTeX-mode . preview-larger-previews))
  :config
  (defun preview-larger-previews ()
    (setq preview-scale-function
          (lambda () (* 1.25
                        (funcall (preview-scale-from-face)))))))

;; CDLatex settings
(use-package cdlatex
  :hook (LaTeX-mode . turn-on-cdlatex)
  :bind (:map cdlatex-mode-map
              ("<tab>" . cdlatex-tab)))

;; Yasnippet settings
(use-package yasnippet
  :hook ((LaTeX-mode . yas-minor-mode)
         (post-self-insert . my/yas-try-expanding-auto-snippets))
  :config
  (use-package warnings
    :config
    (cl-pushnew '(yasnippet backquote-change)
                warning-suppress-types
                :test 'equal))

  (setq yas-triggers-in-field t)

  ;; Function that tries to autoexpand YaSnippets
  ;; The double quoting is NOT a typo!
  (defun my/yas-try-expanding-auto-snippets ()
    (when (and (boundp 'yas-minor-mode) yas-minor-mode)
      (let ((yas-buffer-local-condition ''(require-snippet-condition . auto)))
        (yas-expand)))))

;; CDLatex integration with YaSnippet: Allow cdlatex tab to work inside Yas
;; fields
(use-package cdlatex
  :hook ((cdlatex-tab . yas-expand)
         (cdlatex-tab . cdlatex-in-yas-field))
  :config
  (use-package yasnippet
    :bind (:map yas-keymap
                ("<tab>" . yas-next-field-or-cdlatex)
                ("TAB" . yas-next-field-or-cdlatex))
    :config
    (defun cdlatex-in-yas-field ()
      (when-let* ((_ (overlayp yas--active-field-overlay))
                  (end (overlay-end yas--active-field-overlay)))
        (if (>= (point) end)
            (let ((s (thing-at-point 'sexp)))
              (unless (and s (assoc (substring-no-properties s)
                                    cdlatex-command-alist-comb))
                (yas-next-field-or-maybe-expand)
                t))
          ;; otherwise expand and jump to the correct location
          (let (cdlatex-tab-hook minp)
            (setq minp
                  (min (save-excursion (cdlatex-tab)
                                       (point))
                       (overlay-end yas--active-field-overlay)))
            (goto-char minp) t))))

    (defun yas-next-field-or-cdlatex nil
      (interactive)
      "Jump to the next Yas field correctly with cdlatex active."
      (if
          (or (bound-and-true-p cdlatex-mode)
              (bound-and-true-p org-cdlatex-mode))
          (cdlatex-tab)
        (yas-next-field-or-maybe-expand)))))

;; Array/tabular input with org-tables and cdlatex
(use-package org-table
  :after cdlatex
  :bind (:map orgtbl-mode-map
              ("<tab>" . lazytab-org-table-next-field-maybe)
              ("TAB" . lazytab-org-table-next-field-maybe))
  :init
  (add-hook 'cdlatex-tab-hook 'lazytab-cdlatex-or-orgtbl-next-field 90)
  ;; Tabular environments using cdlatex
  (add-to-list 'cdlatex-command-alist '("smat" "Insert smallmatrix env"
                                        "\\left( \\begin{smallmatrix} ? \\end{smallmatrix} \\right)"
                                        lazytab-position-cursor-and-edit
                                        nil nil t))
  (add-to-list 'cdlatex-command-alist '("bmat" "Insert bmatrix env"
                                        "\\begin{bmatrix} ? \\end{bmatrix}"
                                        lazytab-position-cursor-and-edit
                                        nil nil t))
  (add-to-list 'cdlatex-command-alist '("pmat" "Insert pmatrix env"
                                        "\\begin{pmatrix} ? \\end{pmatrix}"
                                        lazytab-position-cursor-and-edit
                                        nil nil t))
  (add-to-list 'cdlatex-command-alist '("tbl" "Insert table"
                                        "\\begin{table}\n\\centering ? \\caption{}\n\\end{table}\n"
                                        lazytab-position-cursor-and-edit
                                        nil t nil))
  :config
  ;; Tab handling in org tables
  (defun lazytab-position-cursor-and-edit ()
    ;; (if (search-backward "\?" (- (point) 100) t)
    ;;     (delete-char 1))
    (cdlatex-position-cursor)
    (lazytab-orgtbl-edit))

  (defun lazytab-orgtbl-edit ()
    (advice-add 'orgtbl-ctrl-c-ctrl-c :after #'lazytab-orgtbl-replace)
    (orgtbl-mode 1)
    (open-line 1)
    (insert "\n|"))

  (defun lazytab-orgtbl-replace (_)
    (interactive "P")
    (unless (org-at-table-p) (user-error "Not at a table"))
    (let* ((table (org-table-to-lisp))
           params
           (replacement-table
            (if (texmathp)
                (lazytab-orgtbl-to-amsmath table params)
              (orgtbl-to-latex table params))))
      (kill-region (org-table-begin) (org-table-end))
      (open-line 1)
      (push-mark)
      (insert replacement-table)
      (align-regexp (region-beginning) (region-end) "\\([:space:]*\\)& ")
      (orgtbl-mode -1)
      (advice-remove 'orgtbl-ctrl-c-ctrl-c #'lazytab-orgtbl-replace)))

  (defun lazytab-orgtbl-to-amsmath (table params)
    (orgtbl-to-generic
     table
     (org-combine-plists
      '(:splice t
        :lstart ""
        :lend " \\\\"
        :sep " & "
        :hline nil
        :llend "")
      params)))

  (defun lazytab-cdlatex-or-orgtbl-next-field ()
    (when (and (bound-and-true-p orgtbl-mode)
               (org-table-p)
               (looking-at "[[:space:]]*\\(?:|\\|$\\)")
               (let ((s (thing-at-point 'sexp)))
                 (not (and s (assoc s cdlatex-command-alist-comb)))))
      (call-interactively #'org-table-next-field)
      t))

  (defun lazytab-org-table-next-field-maybe ()
    (interactive)
    (if (bound-and-true-p cdlatex-mode)
        (cdlatex-tab)
      (org-table-next-field))))

;; Calctex
(use-package! calctex
  :commands calctex-mode
  :init
  (add-hook 'calc-mode-hook #'calctex-mode)
  :config
  (setq calctex-additional-latex-packages "
\\usepackage[usenames]{xcolor}
\\usepackage{soul}
\\usepackage{adjustbox}
\\usepackage{amsmath}
\\usepackage{amssymb}
\\usepackage{siunitx}
\\usepackage{cancel}
\\usepackage{mathtools}
\\usepackage{mathalpha}
\\usepackage{xparse}
\\usepackage{arevmath}"
        calctex-additional-latex-macros
        (concat calctex-additional-latex-macros
                "\n\\let\\evalto\\Rightarrow"))
  (defadvice! no-messaging-a (orig-fn &rest args)
    :around #'calctex-default-dispatching-render-process
    (let ((inhibit-message t) message-log-max)
      (apply orig-fn args)))
  ;; Fix hardcoded dvichop path (whyyyyyyy)
  (let ((vendor-folder (concat (file-truename doom-local-dir)
                               "straight/"
                               (format "build-%s" emacs-version)
                               "/calctex/vendor/")))
    (setq calctex-dvichop-sty (concat vendor-folder "texd/dvichop")
          calctex-dvichop-bin (concat vendor-folder "texd/dvichop")))
  (unless (file-exists-p calctex-dvichop-bin)
    (message "CalcTeX: Building dvichop binary")
    (let ((default-directory (file-name-directory calctex-dvichop-bin)))
      (call-process "make" nil nil nil))))

(map! :map calc-mode-map
      :after calc
      :localleader
      :desc "Embedded calc (toggle)" "e" #'calc-embedded)
(map! :map org-mode-map
      :after org
      :localleader
      :desc "Embedded calc (toggle)" "E" #'calc-embedded)
(map! :map latex-mode-map
      :after latex
      :localleader
      :desc "Embedded calc (toggle)" "e" #'calc-embedded)

(defvar calc-embedded-trail-window nil)
(defvar calc-embedded-calculator-window nil)

(defadvice! calc-embedded-with-side-pannel (&rest _)
  :after #'calc-do-embedded
  (when calc-embedded-trail-window
    (ignore-errors
      (delete-window calc-embedded-trail-window))
    (setq calc-embedded-trail-window nil))
  (when calc-embedded-calculator-window
    (ignore-errors
      (delete-window calc-embedded-calculator-window))
    (setq calc-embedded-calculator-window nil))
  (when (and calc-embedded-info
             (> (* (window-width) (window-height)) 1200))
    (let ((main-window (selected-window))
          (vertical-p (> (window-width) 80)))
      (select-window
       (setq calc-embedded-trail-window
             (if vertical-p
                 (split-window-horizontally (- (max 30 (/ (window-width) 3))))
               (split-window-vertically (- (max 8 (/ (window-height) 4)))))))
      (switch-to-buffer "*Calc Trail*")
      (select-window
       (setq calc-embedded-calculator-window
             (if vertical-p
                 (split-window-vertically -6)
               (split-window-horizontally (- (/ (window-width) 2))))))
      (switch-to-buffer "*Calculator*")
      (select-window main-window))))

(setq TeX-save-query nil
      TeX-show-compilation t
      TeX-command-extra-options "-shell-escape")
(after! latex
  (add-to-list 'TeX-command-list '("XeLaTeX" "%`xelatex%(mode)%' %t" TeX-run-TeX nil t)))

(setq tec/yas-latex-template-preamble "
<<latex-nice-preamble>>
")

(defun tec/yas-latex-get-class-choice ()
  "Prompt user for LaTeX class choice"
  (setq tec/yas-latex-class-choice (completing-read "Select document class: " '("article" "scrartcl" "bmc"))))

(defun tec/yas-latex-preamble-if ()
  "Based on class choice prompt for insertion of default preamble"
  (if (equal tec/yas-latex-class-choice "bmc") 'nil
    (eq (read-char-choice "Include default preamble? [Type y/n]" '(?y ?n)) ?y)))

(after! tex
  (defvar tec/tex-last-delim-char nil)
  (defvar tec/tex-delim-dot-second t)
  (defun tec/get-open-delim-char ()
    "Exclusivly read next char to tec/tex-last-delim-char"
    (setq tec/tex-delim-dot-second nil)
    (setq tec/tex-last-delim-char (read-char-exclusive "Opening deliminator, recognises: 9 ( [ { < | ."))
    (when (eql ?. tec/tex-last-delim-char)
      (setq tec/tex-delim-dot-second (read-char-exclusive "Other deliminator, recognises: 0 9 (  ) [ ] { } < > |"))))
  (defun tec/tex-open-delim-from-char (&optional open-char)
    "Find the associated opening delim as string"
    (unless open-char (setq open-char (if (eql ?. tec/tex-last-delim-char)
                                          tec/tex-delim-dot-second
                                        tec/tex-last-delim-char)))
    (pcase open-char
      (?\( "(")
      (?9  "(")
      (?\[ "[")
      (?\{ "\\{")
      (?<  "<")
      (?|  (if tec/tex-delim-dot-second "." "|"))
      (_   ".")))
  (defun tec/tex-close-delim-from-char (&optional open-char)
    "Find the associated closing delim as string"
    (if tec/tex-delim-dot-second
        (pcase tec/tex-delim-dot-second
          (?\) ")")
          (?0  ")")
          (?\] "]")
          (?\} "\\}")
          (?\> ">")
          (?|  "|")
          (_   "."))
      (pcase (or open-char tec/tex-last-delim-char)
        (?\( ")")
        (?9  ")")
        (?\[ "]")
        (?\{ "\\}")
        (?<  ">")
        (?\) ")")
        (?0  ")")
        (?\] "]")
        (?\} "\\}")
        (?\> ">")
        (?|  "|")
        (_   "."))))
  (defun tec/tex-next-char-smart-close-delim (&optional open-char)
    (and (bound-and-true-p smartparens-mode)
         (eql (char-after) (pcase (or open-char tec/tex-last-delim-char)
                             (?\( ?\))
                             (?\[ ?\])
                             (?{ ?})
                             (?< ?>)))))
  (defun tec/tex-delim-yas-expand (&optional open-char)
    (yas-expand-snippet (yas-lookup-snippet "_deliminators" 'latex-mode) (point) (+ (point) (if (tec/tex-next-char-smart-close-delim open-char) 2 1)))))

(after! latex
  (setcar (assoc "‚ãÜ" LaTeX-fold-math-spec-list) "‚òÖ")) ;; make \star bigger

(setq TeX-fold-math-spec-list
      `(;; missing/better symbols
        ("‚â§" ("le"))
        ("‚â•" ("ge"))
        ("‚â†" ("ne"))
        ;; convenience shorts -- these do not work nicely ATM
        ;; ("‚Äπ" ("left"))
        ;; ("‚Ä∫" ("right"))
        ;; private macros
        ("‚Ñù" ("RR"))
        ("‚Ñï" ("NN"))
        ("‚Ñ§" ("ZZ"))
        ("‚Ñö" ("QQ"))
        ("‚ÑÇ" ("CC"))
        ("‚Ñô" ("PP"))
        ("‚Ñç" ("HH"))
        ("ùîº" ("EE"))
        ("ùëë" ("dd"))
        ;; known commands
        ("" ("phantom"))
        (,(lambda (num den) (if (and (TeX-string-single-token-p num) (TeX-string-single-token-p den))
                                (concat num "Ôºè" den)
                              (concat "‚ù™" num "Ôºè" den "‚ù´"))) ("frac"))
        (,(lambda (arg) (concat "‚àö" (TeX-fold-parenthesize-as-necessary arg))) ("sqrt"))
        (,(lambda (arg) (concat "‚≠°" (TeX-fold-parenthesize-as-necessary arg))) ("vec"))
        ("‚Äò{1}‚Äô" ("text"))
        ;; private commands
        ("|{1}|" ("abs"))
        ("‚Äñ{1}‚Äñ" ("norm"))
        ("‚åä{1}‚åã" ("floor"))
        ("‚åà{1}‚åâ" ("ceil"))
        ("‚åä{1}‚åâ" ("round"))
        ("ùëë{1}/ùëë{2}" ("dv"))
        ("‚àÇ{1}/‚àÇ{2}" ("pdv"))
        ;; fancification
        ("{1}" ("mathrm"))
        (,(lambda (word) (string-offset-roman-chars 119743 word)) ("mathbf"))
        (,(lambda (word) (string-offset-roman-chars 119951 word)) ("mathcal"))
        (,(lambda (word) (string-offset-roman-chars 120003 word)) ("mathfrak"))
        (,(lambda (word) (string-offset-roman-chars 120055 word)) ("mathbb"))
        (,(lambda (word) (string-offset-roman-chars 120159 word)) ("mathsf"))
        (,(lambda (word) (string-offset-roman-chars 120367 word)) ("mathtt"))
        )
      TeX-fold-macro-spec-list
      '(
        ;; as the defaults
        ("[f]" ("footnote" "marginpar"))
        ("[c]" ("cite"))
        ("[l]" ("label"))
        ("[r]" ("ref" "pageref" "eqref"))
        ("[i]" ("index" "glossary"))
        ("..." ("dots"))
        ("{1}" ("emph" "textit" "textsl" "textmd" "textrm" "textsf" "texttt"
                "textbf" "textsc" "textup"))
        ;; tweaked defaults
        ("¬©" ("copyright"))
        ("¬Æ" ("textregistered"))
        ("‚Ñ¢"  ("texttrademark"))
        ("[1]:||‚ñ∫" ("item"))
        ("‚ù°‚ù°‚ÄÜ{1}" ("part" "part*"))
        ("‚ù°‚ÄÜ{1}" ("chapter" "chapter*"))
        ("¬ß‚ÄÜ{1}" ("section" "section*"))
        ("¬ß¬ß‚ÄÜ{1}" ("subsection" "subsection*"))
        ("¬ß¬ß¬ß‚ÄÜ{1}" ("subsubsection" "subsubsection*"))
        ("¬∂‚ÄÜ{1}" ("paragraph" "paragraph*"))
        ("¬∂¬∂‚ÄÜ{1}" ("subparagraph" "subparagraph*"))
        ;; extra
        ("‚¨ñ‚ÄÜ{1}" ("begin"))
        ("‚¨ó‚ÄÜ{1}" ("end"))
        ))

(defun string-offset-roman-chars (offset word)
  "Shift the codepoint of each character in WORD by OFFSET with an extra -6 shift if the letter is lowercase"
  (apply 'string
         (mapcar (lambda (c)
                   (string-offset-apply-roman-char-exceptions
                    (+ (if (>= c 97) (- c 6) c) offset)))
                 word)))

(defvar string-offset-roman-char-exceptions
  '(;; lowercase serif
    (119892 .  8462) ; ‚Ñé
    ;; lowercase caligraphic
    (119994 . 8495) ; ‚ÑØ
    (119996 . 8458) ; ‚Ñä
    (120004 . 8500) ; ‚Ñ¥
    ;; caligraphic
    (119965 . 8492) ; ‚Ñ¨
    (119968 . 8496) ; ‚Ñ∞
    (119969 . 8497) ; ‚Ñ±
    (119971 . 8459) ; ‚Ñã
    (119972 . 8464) ; ‚Ñê
    (119975 . 8466) ; ‚Ñí
    (119976 . 8499) ; ‚Ñ≥
    (119981 . 8475) ; ‚Ñõ
    ;; fraktur
    (120070 . 8493) ; ‚Ñ≠
    (120075 . 8460) ; ‚Ñå
    (120076 . 8465) ; ‚Ñë
    (120085 . 8476) ; ‚Ñú
    (120092 . 8488) ; ‚Ñ®
    ;; blackboard
    (120122 . 8450) ; ‚ÑÇ
    (120127 . 8461) ; ‚Ñç
    (120133 . 8469) ; ‚Ñï
    (120135 . 8473) ; ‚Ñô
    (120136 . 8474) ; ‚Ñö
    (120137 . 8477) ; ‚Ñù
    (120145 . 8484) ; ‚Ñ§
    )
  "An alist of deceptive codepoints, and then where the glyph actually resides.")

(defun string-offset-apply-roman-char-exceptions (char)
  (if (assoc char string-offset-roman-char-exceptions)
      (cdr (assoc char string-offset-roman-char-exceptions))
    char))

(defun TeX-fold-parenthesize-as-necessary (tokens &optional suppress-left suppress-right)
  (if (TeX-string-single-token-p tokens) tokens
    (concat (if suppress-left "" "‚ù™")
            tokens
            (if suppress-right "" "‚ù´"))))

(defun TeX-string-single-token-p (teststring)
  "Return t if TESTSTRING appears to be a single token, nil otherwise"
  (if (string-match-p "^\\\\?\\w+$" teststring) t nil))

(after! tex
  (map!
   :map LaTeX-mode-map
   :ei [C-return] #'LaTeX-insert-item)
  (setq TeX-electric-math '("\\(" . "")))

;; Making \( \) less visible
(defface unimportant-latex-face
  '((t :inherit font-lock-comment-face :weight extra-light))
  "Face used to make \\(\\), \\[\\] less visible."
  :group 'LaTeX-math)

(font-lock-add-keywords
 'latex-mode
 `(("\\\\[]()[]" 0 'unimportant-latex-face prepend))
 'end)

(setq preview-LaTeX-command '("%`%l \"\\nonstopmode\\nofiles\
\\PassOptionsToPackage{" ("," . preview-required-option-list) "}{preview}\
\\AtBeginDocument{\\ifx\\ifPreview\\undefined"
preview-default-preamble "\\fi}\"%' \"\\detokenize{\" %t \"}\""))

(after! cdlatex
  (setq cdlatex-env-alist
        '(("bmatrix" "\\begin{bmatrix}\n?\n\\end{bmatrix}" nil)
          ("equation*" "\\begin{equation*}\n?\n\\end{equation*}" nil)))
  (setq ;; cdlatex-math-symbol-prefix ?\; ;; does not work at the moment :(
   cdlatex-math-symbol-alist
   '( ;; adding missing functions to 3rd level symbols
     (?_    ("\\downarrow"  ""           "\\inf"))
     (?2    ("^2"           "\\sqrt{?}"     ""     ))
     (?3    ("^3"           "\\sqrt[3]{?}"  ""     ))
     (?^    ("\\uparrow"    ""           "\\sup"))
     (?k    ("\\kappa"      ""           "\\ker"))
     (?m    ("\\mu"         ""           "\\lim"))
     (?c    (""             "\\circ"     "\\cos"))
     (?d    ("\\delta"      "\\partial"  "\\dim"))
     (?D    ("\\Delta"      "\\nabla"    "\\deg"))
     ;; no idea why \Phi isnt on F in first place, \phi is on f.
     (?F    ("\\Phi"))
     ;; now just convenience
     (?.    ("\\cdot" "\\dots"))
     (?:    ("\\vdots" "\\ddots"))
     (?*    ("\\times" "\\star" "\\ast")))
   cdlatex-math-modify-alist
   '( ;; my own stuff
     (?B    "\\mathbb"        nil          t    nil  nil)
     (?a    "\\abs"           nil          t    nil  nil))))

(use-package! laas
  :hook (LaTeX-mode . laas-mode)
  :config
  (defun laas-tex-fold-maybe ()
    (unless (equal "/" aas-transient-snippet-key)
      (+latex-fold-last-macro-a)))
  (add-hook 'aas-post-snippet-expand-hook #'laas-tex-fold-maybe))

(when EMACS28+
  (add-hook 'latex-mode-hook #'TeX-latex-mode))

;; Currenty not working :(
;; ;; Change the colorscheme
;; (use-package! catppuccin-theme
;;   :custom
;;   (catppuccin-flavor 'frappe)
;;   :config
;;   (setq doom-theme 'catppuccin)
;;   (catppuccin-reload))
